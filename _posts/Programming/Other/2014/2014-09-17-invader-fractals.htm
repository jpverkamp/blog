title: Invader Fractals
date: 2014-09-17 09:00:00
categories:
- Programming/By Language/Racket
- Programming/By Language/Scheme
- Programming/By Topic/Graphics
- Programming/By Topic/Games
- Programming/By Topic/Procedurally Generated Content
- Programming/By Topic/Mathematics/Fractals
comments: true
---
First we had @crosslink{Procedural Invaders}. Then we used them fill up space with @crosslink{Fractal Invaders}. But we're not <em>quite</em> done yet! This time, let's mix things up a bit and make Invader Fractals.

@embed{invader-fractal-3.png}

<!--more-->

Specifically, here's the algorithm that we want to use to build an invader fractal:

<ol>
    <li>Generate an invader</li>
    <li>Within that image:
        <ul>
            <li>For each white pixel, stop</li>
            <li>For each black pixel, recur with a smaller invader</li>
        </ul>
    </li>
    <li>Once the invader is small enough, stop recurring</li>
</ol>

Sounds straight forward enough, but what does that look like in code?

@codeblock["scheme"]|{
; An invader fractal is nested 5x5 2d vectors, each element is either
; #t/#f - a white/black region
; a recursive 5x5 structure
(define (make-invader-fractal depth)
  (let loop ([depth depth])
    (define invader (flomap-add-margin (procedural-invader (random 32768)) 1))
    (for/vector ([x (in-range 7)])
      (for/vector ([y (in-range 7)])
        (if (> 0.5 (flomap-ref invader 0 x y))
            (if (<= depth 1)
                #t
                (loop (- depth 1)))
            #f)))))
}|

That's actually a lot more concise than I first expected. In the end, we get exactly that nice nested structure we're looking for:

@codeblock["scheme"]|{
> (make-invader-fractal 2)
'#(#(#f #f #f #f #f #f #f)
   #(#f
     #(#(#f #f #f #f #f #f #f)
       #(#f #f #t #f #t #f #f)
       #(#f #f #f #f #t #f #f)
       #(#f #t #t #f #f #t #f)
       #(#f #f #f #f #t #f #f)
       #(#f #f #t #f #t #f #f)
       #(#f #f #f #f #f #f #f))
     #f
     #(#(#f #f #f #f #f #f #f)
       #(#f #t #t #f #t #t #f)
       #(#f #f #t #t #f #f #f)
       #(#f #f #f #t #t #f #f)
       #(#f #f #t #t #f #f #f)
       #(#f #t #t #f #t #t #f)
       #(#f #f #f #f #f #f #f))
     #f
...
     #f
     #f
     #f)
   #(#f #f #f #f #f #f #f))
>
}|

We could probably do away with representing the margins (the outermost layer is always going to be `#f`), but at the moment it makes the code easier to reason about.

Okay, next we need the rendering function:

@codeblock["scheme"]|{
; Render an invader fractal as defined above
; Crop off the margin on the outmost layer
; Final size will be 5*7^{depth-1}
(define (render-invader-fractal fi)
  (define depth
    (let loop ([fi fi])
      (cond
        [(boolean? fi) 0]
        [else
         (+ 1 (for*/fold ([deepest 0]) ([col (in-vector fi)]
                                        [el (in-vector col)])
                (max (loop el) deepest)))])))

  (define size (expt 7 depth))

  (flomap->bitmap
   (flomap-crop
    (build-flomap*
     1 size size
     (Î» (x y)
       (let loop ([t 0] [l 0] [s size] [fi fi])
         (cond
           [(eq? fi #t) '#(0.0)]
           [(eq? fi #f) '#(1.0)]
           [else
            ; xi and yi are the points within the current level invader
            (define xi (quotient (* 7 (- x l)) s))
            (define yi (quotient (* 7 (- y t)) s))
            (loop (+ t (* yi (/ s 7)))
                  (+ l (* xi (/ s 7)))
                  (/ s 7)
                  (vector-ref (vector-ref fi xi) yi))]))))
    (* size 5/7)
    (* size 5/7)
    1/2
    1/2)))
}|

Unfortunately, that first bit is a little bit hacky. Since we've split apart the functions that create and render this fractal, we don't know how large of an image to make. Still, it's quick enough to calculate. Then, we get into actually making the image. It's much the same as the code in @crosslink{Fractal Invaders}. We recur down, keeping two different sets of coordinates: image coorcinates `x`, `y`, `t`, `l`, and `s` and then coordinates within the current level `xi` and `yi`. There is a lot of dividing and multiplying by that @wikipedia{magic number}, but os it goes.

And that's really it. There's a call to @racket-doc{flomap-crop}, but that's just to cut off the outermost margin (since it will (1) always be empty and (2) the second level's margin will still include some spacing). So how does it look?

@codeblock["scheme"]|{ > (render-invader-fractal (make-invader-fractal 3)) }|

@embed{invader-fractal-3.png}

That's about it. One more trick that I want to do though is to make these reproducable. That we can do some neat tricks with zooming in:

@codeblock["scheme"]|{
(define-syntax-rule (with-seed seed body* ...)
  (parameterize ([current-pseudo-random-generator
                  (make-pseudo-random-generator)])
    (random-seed seed)
    body* ...))

(define (invader-fractal i depth)
  (with-seed i
    (render-invader-fractal (make-invader-fractal depth))))
}|

Because we reset the random seed at the beginning of the calculate and always generate the random numbers from top to bottom, we ned up with some neat effects. The top level with the same seed is always the same shape:

@codeblock["scheme"]|{
> (for/list ([depth (in-range 1 5)])
    (invader-fractal 8675309 depth))
}|

@embed{invader-fractal-seeded-1.png}
@embed{invader-fractal-seeded-2.png}
@embed{invader-fractal-seeded-3.png}
@embed[#:width 245 #:height 245]{invader-fractal-seeded-4.png}
@footnote{Click to embiggen.}

:)

Unfortunately, the lower levels aren't the same pattern, since we're generating the images using a @wikipedia{depth first search} rather than the @wikipedia{breadth first search}. More specifically, we're generating the invaders in this order:

@embed{depth-first.png}

In that case, the ordering is: generate the image for the given level, then the first subimage, then the first subimage of that. Generating the <em>depth first</em> as it were. What we want instead is to generate generate the top level, then all of the next level, then all of the next level, more like this:

@embed{breadth-first.png}

Unfortunately, that doesn't work out well in our specific code, since we're working recursively, working with a @wikipedia["Stack (computer science)"]{stack} (implicitly via function calls). To do a breadth first search, we would need instead to create a explicit @wikipedia["Stack (computer science)"]{queue}, which would require some fairly major refactoring.

There's another option though:

@codeblock["scheme"]|{
; An invader fractal is nested 5x5 2d vectors, each element is either
; #t/#f - a white/black region
; a recursive 5x5 structure
(define (make-invader-fractal/seeded seed depth)
  (define (mod-random-seed! i)
    (random-seed (+ 1 (remainder i 2147483646))))

  (mod-random-seed! seed)
  (let loop ([d depth])
    (define invader (flomap-add-margin (procedural-invader (random 32768)) 1))
    (for/vector ([x (in-range 7)])
      (for/vector ([y (in-range 7)])
        (if (> 0.5 (flomap-ref invader 0 x y))
            (if (<= d 1)
                #t
                (begin
                  (mod-random-seed! (+ (* (- depth d) 49) (* x 7) y seed))
                  (loop (- d 1))))
            #f)))))

(define (invader-fractal seed depth)
  (render-invader-fractal (make-invader-fractal/seeded seed depth)))
}|

With the addition of `mod-random-seed!`, we can set the seed at each level. And we've got the amusing notion of non-random random seeds. :) But since they're generated as a function of the current `depth` and `x` and `y` coordinates of the parent image, we'll always get the same images at each level:

@codeblock["scheme"]|{
> (for/list ([depth (in-range 1 5)])
    (invader-fractal 42 depth))
}|

@embed{invader-fractal-better-seeded-1.png}
@embed{invader-fractal-better-seeded-2.png}
@embed{invader-fractal-better-seeded-3.png}
@embed[#:width 245 #:height 245]{invader-fractal-better-seeded-4.png}

Now that's what I was looking for. :)

As always, today's code is available on GitHub. Check it out: <a href="https://github.com/jpverkamp/small-projects/blob/master/blog/invader-fractals.rkt">invader-fractals.rkt</a> (Requires <a href="https://github.com/jpverkamp/small-projects/blob/master/blog/procedural-invaders.rkt">procedural-invaders.rkt</a> to run.)

Challenge: Make fractal invaders from invader fractals. Maybe later... O:)
